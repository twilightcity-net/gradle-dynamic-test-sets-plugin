plugins {
    id "groovy"
    id "java-gradle-plugin"
    id "idea"
    id 'maven-publish'
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

java {
    withSourcesJar()
}

dependencies {
    api "org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:4.0.0"

    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}

String pluginDescription = "Opinionated, convention over configuration test plugin.  Using https://github.com/unbroken-dome/gradle-testsets-plugin " +
        "for the heavy lifting, this plugin inspects the source trees of a project and automatically configures " +
        "gradle-testsets-plugin based on directory naming conventions."

gradlePlugin {
    plugins {
        dynamicTestSets {
            id = "net.twilightcity.dynamic-test-sets"
            displayName = "Dynamic TestSets"
            description = pluginDescription
            implementationClass = "org.betterdevxp.gradle.test.DynamicTestSetsPlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            url "${findProperty('nexus.url')}/repository/twilightcity"
            credentials {
                username findProperty('nexus.username')
                password findProperty('nexus.password')
            }
        }
    }
}
